function timezsh() {
    for i in $(seq 1 10); do time zsh -i -c 'exit'; done
}

function brew() {
  command brew "$@"

  if [[ $* =~ "upgrade" ]] || [[ $* =~ "update" ]] || [[ $* =~ "outdated" ]]; then
    sketchybar --trigger brew_update
  fi
}

function setup_gpg_agent() {
	envfile="$HOME/.gnupg/gpg-agent.env"
	if [[ -e "$envfile" ]] && kill -0 $(grep GPG_AGENT_INFO "$envfile" | cut -d: -f 2) 2>/dev/null; then
		eval "$(cat "$envfile")"
	else
		eval "$(gpg-agent --daemon --enable-ssh-support --write-env-file "$envfile")"
	fi
	export GPG_AGENT_INFO  # the env file does not contain the export statement
	export SSH_AUTH_SOCK   # enable gpg-agent for ssh
	export SSH_AGENT_PID
}

# Simple calculator
function calc() {
        local result=""
        result="$(printf "scale=10;$*\n" | bc --mathlib | tr -d '\\\n')"
        #                       └─ default (when `--mathlib` is used) is 20
        #
        if [[ "$result" == *.* ]]; then
                # improve the output for decimal numbers
                printf "$result" |
                sed -e 's/^\./0./'        `# add "0" for cases like ".5"` \
                    -e 's/^-\./-0./'      `# add "0" for cases like "-.5"`\
                    -e 's/0*$//;s/\.$//'   # remove trailing zeros
        else
                printf "$result"
        fi
        printf "\n"
}

# homeshick
function homeshick () {
  if [ "$1" = "cd" ] && [ -n "$2" ]
  then
    cd "$HOME/.homesick/repos/$2"
  else
    $HOME/.homesick/repos/homeshick/bin/homeshick "$@"
  fi
}

function convert-raw-to-jpg() {
  local quality=${1:-80};
  local max_dim=${2:-2650};
  local source_files=${3:-\*.CR2};
  echo "Usage: convert-raw-to-jpg [quality=80] [max-dimension-px=2560] [source=\*.RAW]";
  echo "Converting all ${source_files} to JPEG (${quality} quality, ${max_dim}px max) to output/...";
  mkdir -p output 2> /dev/null;
  find . -type f -iname "${source_files}" -print0 | \
      xargs -0 -n 1 -P 8 -I {} convert -verbose -units PixelsPerInch {} \
      -colorspace sRGB -resize ${max_dim}x${max_dim} -set filename:new '%t-%wx%h' \
      -density 72 -format JPG -quality ${quality} 'output/%[filename:new].jpg';
  echo 'Done.';
}

# Disable globbing on the remote path.
alias scp='noglob scp_wrap'
function scp_wrap {
  local -a args
  local i
  for i in "$@"; do case $i in
    (*:*) args+=($i) ;;
    (*) args+=(${~i}) ;;
  esac; done
  command scp "${(@)args}"
}

function runbg {
  nohup $* > /dev/null 2>&1 &
}

function weather {
  local city=$(curl -s https://ipvigilante.com/$(curl -s https://ipinfo.io/ip) | jq -r '.data.city_name')
  curl --silent "wttr.in/${city}?m" | grep -v Follow
}
